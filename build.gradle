buildscript {
  repositories {
    maven { url = "https://files.minecraftforge.net/maven" }
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
  }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = "knightminer"
archivesBaseName = "Ceramics"

configurations {
    deployerJars
}

version = "${minecraft_version}-${mod_version}"

sourceSets {
  main { resources { srcDirs "$rootDir/src/generated/resources" } }
}

minecraft {
  mappings channel: 'snapshot', version: mappings_version
  accessTransformer project.file("src/main/resources/META-INF/accesstransformer.cfg")
  runs {
    client {
      workingDirectory project.file('run')
      // Recommended logging data for a userdev environment
      property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
      // Recommended logging level for the console
      property 'forge.logging.console.level', 'debug'
      mods {
        ceramics {
          source sourceSets.main
        }
      }
    }

    server {
      workingDirectory project.file('run')
      // Recommended logging data for a userdev environment
      property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
      // Recommended logging level for the console
      property 'forge.logging.console.level', 'debug'
      mods {
        ceramics {
          source sourceSets.main
        }
      }
    }

    data {
      workingDirectory project.file('run')
      // Recommended logging data for a userdev environment
      property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
      // Recommended logging level for the console
      property 'forge.logging.console.level', 'debug'
      args '--mod', 'ceramics', '--all', '--output', file('src/generated/resources/')
      mods {
        ceramics {
           source sourceSets.main
        }
      }
    }
  }
}

// Need this here so eclipse task generates correctly.
sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

repositories {
  // JEI and Mantle
  maven {
    name 'DVS1 Maven FS'
    url 'http://dvs1.progwml6.com/files/maven'
  }
}

dependencies {
  minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
  compile fg.deobf("slimeknights.mantle:Mantle:${minecraft_version}-${mantle_version}")
    
  // compile against the JEI API but do not include it at runtime
  compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
  // at runtime, use the full JEI jar
  runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")
}

// replace properties in mods.toml with properties from config
processResources {
  inputs.property "version", mod_version
  inputs.property "minecraft_version", minecraft_version

  from(sourceSets.main.resources.srcDirs) {
    include 'META-INF/mods.toml'
    expand 'version': mod_version, 'javafml': javafml_min,
        'minecraft_version': minecraft_version, 'minecraft_min': minecraft_min,
        'forge_version': forge_min, 'mantle_version': mantle_version
  }

  from(sourceSets.main.resources.srcDirs) {
    exclude 'META-INF/mods.toml'
  }
}

jar {
  manifest {
    attributes([
      "Specification-Title": "Ceramics",
      "Specification-Vendor": "KnightMiner",
      "Specification-Version": "1", // We are version 1 of ourselves
      "Implementation-Title": project.name,
      "Implementation-Version": "${version}",
      "Implementation-Vendor": "KnightMiner",
      "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}

// because the normal output has been made to be obfuscated
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}